buildscript {
  apply from: "${rootProject.rootDir}/_buildsystem/buildscript.gradle"
  repositories project.ext.classpathRepositories
  dependencies project.ext.classpathDependencies
}

apply from: "${rootProject.rootDir}/_buildsystem/configurations.gradle"
apply from: "${rootProject.rootDir}/_buildsystem/dependencies.gradle"
apply from: "${rootProject.rootDir}/_buildsystem/properties.gradle"
apply from: "${rootProject.rootDir}/_buildsystem/optimizations.gradle"
apply from: "${rootProject.rootDir}/_buildsystem/test_options.gradle"

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "org.jmailen.kotlinter"

android {
  buildToolsVersion project.ext.buildToolsVersion
  compileSdkVersion project.ext.androidCompileSdkVersion
  defaultConfig {
    versionCode project.ext.androidVersionCode
    versionName project.ext.androidVersionName
    minSdkVersion project.ext.androidMinSdkVersion
    targetSdkVersion project.ext.androidTargetSdkVersion
    buildConfigField "String", "PLATFORM_ANDROID", "\"android\""
    buildConfigField "String", "TINDER_VERSION_NAME", "\"11.18.0\""
    buildConfigField "String", "TINDER_API_LOCALE", "\"en\""
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
            "room.schemaLocation": "${rootProject.projectDir}/_assets/db".toString()
        ]
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
  dexOptions {
    preDexLibraries false
    javaMaxHeapSize "8g"
  }
  lintOptions {
    ignoreTestSources true
    abortOnError true
    absolutePaths true
    checkAllWarnings true
    disable "WrongThreadInterprocedural"
    checkDependencies true
    htmlOutput = new File("$project.ext.staticAnalysisReportTarget/$project.name/lint.html")
    quiet false
    warningsAsErrors true
  }
  packagingOptions {
    exclude "META-INF/services/javax.annotation.processing.Processor"
  }
  sourceSets.forEach {
    it.java.srcDirs = ["src/$it.name/kotlin"]
  }
  testOptions project.ext.testOptions
}
dependencies {
  implementation project(":domain")
  implementation project(":crash-reporter")
  implementation project(":event-tracker")
  implementation project(":extensions")
  implementation project(":iaps")
  implementation project(":translations")
  kapt project.ext.annotationProcessorDependencies
  debugImplementation project.ext.debugImplementationDataDependencies
  implementation project.ext.implementationDependencies
  implementation project.ext.implementationDataDependencies
  compileOnly project.ext.providedDependencies
}
